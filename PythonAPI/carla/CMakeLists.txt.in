cmake_minimum_required (
  VERSION @CMAKE_MAJOR_VERSION@.@CMAKE_MINOR_VERSION@
)

cmake_policy (SET CMP0097 NEW)
cmake_policy (SET CMP0091 NEW)
cmake_policy (SET CMP0074 NEW)
cmake_policy (SET CMP0077 NEW)
cmake_policy (SET CMP0117 NEW)
if (${CMAKE_MINOR_VERSION} GREATER_EQUAL 24)
  cmake_policy (SET CMP0135 NEW)
endif ()

project (
  carla-python-api
  LANGUAGES CXX
  VERSION @CARLA_VERSION@
  DESCRIPTION
    "Python API for communicating with the CARLA server."
)

set (CMAKE_CXX_STANDARD @CMAKE_CXX_STANDARD@)
set (CMAKE_CXX_STANDARD_REQUIRED @CMAKE_CXX_STANDARD_REQUIRED@)

set (CMAKE_C_STANDARD @CMAKE_C_STANDARD@)
set (CMAKE_C_STANDARD_REQUIRED @CMAKE_C_STANDARD_REQUIRED@)

find_package (
  Python3
  COMPONENTS
    Interpreter
    Development.Module
    Development.Embed
    Development.SABIModule
  REQUIRED
)

include (@CARLA_WORKSPACE_PATH@/CMake/Util.cmake)

carla_message (
  "Python Interpreter ID: \"${Python3_INTERPRETER_ID}\""
)

set (
  PYTHON_API_SOURCES
  @CARLA_PYTHON_API_CARLA_PATH@/src/PythonAPI.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Actor.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Blueprint.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Client.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Commands.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Control.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Exception.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Geometry.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/LightManager.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Map.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Sensor.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/SensorData.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Snapshot.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/TrafficManager.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/Weather.cpp
  @CARLA_PYTHON_API_CARLA_PATH@/src/World.cpp
)

if (ENABLE_OSM2ODR)
  list (
    APPEND
    PYTHON_API_SOURCES
    @CARLA_PYTHON_API_CARLA_PATH@/src/OSM2ODR.cpp
  )
endif ()

if (ENABLE_RSS)
  list (
    APPEND
    PYTHON_API_SOURCES
    @CARLA_PYTHON_API_CARLA_PATH@/src/AdRss.cpp
  )
endif ()

Python3_add_library (
  carla-python-api
  MODULE
  WITH_SOABI
  ${PYTHON_API_SOURCES}
)

file (
  STRINGS
  @CARLA_PYTHON_API_INCLUDE_LIST_FILE_PATH@
  CARLA_PYTHON_API_INCLUDE_PATHS
)

file (
  STRINGS
  @CARLA_PYTHON_API_LINK_LIST_FILE_PATH@
  CARLA_PYTHON_API_LINK_PATHS
)

list (REMOVE_DUPLICATES CARLA_PYTHON_API_INCLUDE_PATHS)
list (REMOVE_DUPLICATES CARLA_PYTHON_API_LINK_PATHS)

target_include_directories (
  carla-python-api PRIVATE
  @CARLA_PYTHON_API_CARLA_PATH@/include
  ${CARLA_PYTHON_API_INCLUDE_PATHS}
)

target_link_libraries (
  carla-python-api PRIVATE
  ${CARLA_PYTHON_API_LINK_PATHS}
)

target_compile_definitions (
  carla-python-api PRIVATE
  @CARLA_COMMON_DEFINITIONS@
  @CARLA_RTTI_DEFINITIONS@
  BOOST_ALL_NO_LIB
  LIBCARLA_WITH_PYTHON_SUPPORT
)

target_include_directories (
  carla-python-api PRIVATE
  @CARLA_PYTHON_API_CARLA_PATH@/src
  @LIBCARLA_SOURCE_PATH@
)

install (
  TARGETS carla-python-api
  DESTINATION PythonAPI
)
